
/*
This is the core control file of GS_M, the latest incarnation of G_String.
G_String is a java program enabling non-specialist users to perform Generalization Analysis of performance assessments.
GS_M is Open Source.
GS_M is the property of the G_String User Group (gs_users.org).
GS_M is a formal gradle (7.6) project. It runs well on Intellij IDEA.
It is based on modular design, and java versions can be specified in this document.
GS_M builds install packages for the three major OS platforms: Linux, Windows, and IOS.
Consequently GS_M has three versions, one for each of the three platforms.
They only differ in a few lines, dealing with installer packaging. These sections will be commented.
However, it is important to keep the software version synchronized between the platform builds.
Version number has to be updated on: (i) build gradle, (ii) resource /utilities/About.txt.
The versioning convention is as follows:
    version: a.b.c (a, b, and c are integers from 0 incrementing)
    a - major version, incorporating significant functional changes.
    b - changes in java, and javafx jdks.
    c - minor bug fixes and cosmetics.
    Initial alpha version: 0.0.0
The current version of GS_M - 2.1.1 uses updated Gradle 8.5 and Adoptium openjava 21.0.1
 */
plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'

}

java {
    toolchain{
        languageVersion = JavaLanguageVersion.of(21)
    }
}
group 'org.gsusers'
version '2.1.1'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.1'
}

sourceCompatibility = '21.0.1'
targetCompatibility = '21.0.1'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.gsusers.gsmv'
    mainClass = 'org.gsusers.gsmv.GS_Application'
}

javafx {
    version = '21.0.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.1')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
// https://mvnrepository.com/artifact/org.openjfx/javafx-web
    implementation group: 'org.openjfx', name: 'javafx-web', version: '20-ea+11'


    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.gsusers.gsmv.GS_Application'
        )
    }
}

jlink {
    launcher {
        name = 'GS_MV'
    }
    jpackage {
        installerOptions = [
                '--description', 'Generalizability  Analysis',
                '--copyright', 'Copyright 2007-2023 papaworx.com'
        ]
        skipInstaller = false
        vendor = "Open Source"

        //un-comment appropriate installerType


        installerType = "deb"
        imageOptions += ['--icon', 'src/main/resources/org/gsusers/gsmv/GS_MV.png']
        installerOptions += [
                '--linux-deb-maintainer', 'ralph@papaworx.com'
        ]



        /*
            installerType = "msi"
            imageOptions += ['--icon', 'src/main/resources/org/gsusers/gsmv/GS_MV.ico']
            installerOptions += [
                '--win-menu',
                '--win-shortcut'
            ]
        */

        /*
            installerType = "dmg"
            imageOptions += ['--icon', 'src/main/resources/org/gsusers/gsmv/GS_MV.icns']
        */
    }
}